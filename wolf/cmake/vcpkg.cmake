if(NOT DEFINED ENV{VCPKG_ROOT})
    message(FATAL_ERROR "VCPKG_ROOT environment variable is not set.")
endif()

if(DEFINED VCPKG_TARGET_TRIPLET)
    set(vcpkg_triplet ${VCPKG_TARGET_TRIPLET})
elseif(QT_IS_ANDROID_MULTI_ABI_EXTERNAL_PROJECT)
    if(CMAKE_TOOLCHAIN_FILE MATCHES "android_x86_64")
        set(vcpkg_triplet "x64-android")
    elseif(CMAKE_TOOLCHAIN_FILE MATCHES "android_x86")
        set(vcpkg_triplet "x86-android")
    elseif(CMAKE_TOOLCHAIN_FILE MATCHES "android_arm64_v8a")
        set(vcpkg_triplet "arm64-android")
    elseif(CMAKE_TOOLCHAIN_FILE MATCHES "android_armv7")
        set(vcpkg_triplet "arm-neon-android")
    endif()
elseif(ANDROID_ABI)
    if(ANDROID_ABI STREQUAL "x86_64")
        set(vcpkg_triplet "x64-android")
    elseif(ANDROID_ABI STREQUAL "x86")
        set(vcpkg_triplet "x86-android")
    elseif(ANDROID_ABI STREQUAL "arm64-v8a")
        set(vcpkg_triplet "arm64-android")
    elseif(ANDROID_ABI STREQUAL "armeabi-v7a")
        set(vcpkg_triplet "arm-neon-android")
    endif()
else() # desktop
    if(WIN32)
        set(vcpkg_triplet "x64-windows")
    elseif(APPLE)
        set(vcpkg_triplet "x64-osx")
    elseif(UNIX)
        set(vcpkg_triplet "x64-linux")
    endif()
endif()

set(VCPKG_TARGET_TRIPLET ${vcpkg_triplet} CACHE STRING "vcpkg target triplet" FORCE)

include("$ENV{VCPKG_ROOT}/scripts/buildsystems/vcpkg.cmake")

function(vcpkg_install PACKAGE PACKAGE_NAME IS_STATIC)
    message(STATUS "finding ${PACKAGE}")
    find_package(${PACKAGE} CONFIG)
    if(NOT ${PACKAGE}_FOUND)
        message(STATUS "installing ${PACKAGE} via vcpkg")
        if (NOT IS_STATIC)
            execute_process(COMMAND vcpkg install ${PACKAGE_NAME} --triplet=${vcpkg_triplet}) 
        else()
            execute_process(COMMAND vcpkg install ${PACKAGE_NAME}:${vcpkg_triplet}-static)
        endif()
        find_package(${PACKAGE} CONFIG REQUIRED)
    endif()
endfunction()
